from /love use (love, Shader)
use /love as l
use /res/
use /input
use /gfx

use controller as ctrl
use graph
use menu
use guy
from guy use faction_to_int

from /main use varargs

clone :: fn list ->
    list -> map' fn x -> x end
end

// Shuffle, but badly
shuffle :: fn list: [*] do
    if len(list) == 0 ret

    le := len(list)
    list -> for_each' fn _ do
        a :: love.math.random(0, le-1)
        b :: love.math.random(0, le-1)
        temp :: list[a]
        list[a] = list[b]
        list[b] = temp
    end
end

// Colors
BACKGROUND_COLOR :: gfx.rgb' 50. / 255., 50. / 255., 50. / 255.
MENU_COLOR :: gfx.rgb' 1., 1., 1.
HEADER_COLOR :: gfx.rgb' 209. / 255., 59. / 255., 57. / 255.
SELECTED_COLOR :: gfx.rgb' 229. / 255., 126. / 255., 124. / 255.
WHITE :: gfx.rgb' 1., 1., 1.


guy_to_move :: fn state ->
    ret state.guys_to_move[guy.faction_to_int(state.player_turn)] -> last'
end

get_guys_on_node :: fn state, node ->
    guys_on_node :: []
    state.factions -> for_each' fn other_fn do
        ef := guy.faction_to_int(other_fn)
        state.guys[ef] -> for_each' fn other_guy ->
            if other_guy.node == node do
                guys_on_node -> push' other_guy
            end
        end
    end
    guys_on_node
end

count_guys_of_faction :: fn state ->
    ret fn node, faction ->
        gs :: get_guys_on_node' state, node
        gs :: gs -> filter' fn o ->
            guy.faction_to_int(o.faction) == guy.faction_to_int(faction)
        end
        len' gs
    end
end

kill_guy :: fn state, g do
    f :: guy.faction_to_int(g.faction)
    state.guys_to_move[f] = state.guys_to_move[f] -> filter' fn o -> do o != g end
    state.guys[f] = state.guys[f] -> filter' fn o -> do o != g end
end

move_active_guy :: fn state, dir do
    g :: state -> guy_to_move'
    old :: g.node
    new :: graph.facingNeighbour' old, count_guys_of_faction(state), g.faction, dir, state.graph
    if new == -1 do
        state.moves_left = 0
        ret
    end

    guys_on_new_node :: get_guys_on_node' state, new

    friendly_map :: guys_on_new_node -> map' fn o ->
        guy.faction_to_int(o.faction) == guy.faction_to_int(g.faction)
    end

    // Cool visual
    do
        diff :: state.graph.nodes[old].position - state.graph.nodes[new].position
        f :: 25.0
        state.graph.nodes[new].velocity -= diff * (f, f)
    end

    if len(guys_on_new_node) == 0 or friendly_map[0] do
        // Done
        g.node = new
        f :: g.faction
        node :: state.graph.nodes[new]
        node.tile = (fn ->
            case node.tile do
                Captured _ ret graph.Tile.Captured f
                Empty ret graph.Tile.Captured f
                Factory _ ret graph.Tile.Factory f
                EmptyFactory ret graph.Tile.Factory f
                Base f ret graph.Tile.Base f
                else do
                    print' "Remember to add new tile!"
                    <!>
                end
            end
            ret graph.Tile.Empty
        end)()


        if state.moves_left == 0 do
            next_turn' state
        else do
            state.inputState = InputState.MoveGuy
            state.moves_left = state.moves_left - 1
        end
    else if not friendly_map[0] do
        // Kill!
        kill_guy' state, guys_on_new_node[0]
        kill_guy' state, g

        next_turn' state
    end
end


next_turn :: fn state do
    faction_i :: guy.faction_to_int' state.player_turn
    state.guys_to_move[faction_i] -> pop'

    num_factions :: len' state.factions
    next_faction_i :: faction_i + 1
    i :: rem' next_faction_i, num_factions

    state.player_turn = state.factions[i]
    state.moves_left = 1
    state.inputState = InputState.MoveGuy

    fi :: guy.faction_to_int' state.player_turn

    to_move :: state.guys_to_move[fi]

    guys :: state.guys[fi]

    end_of_turn :: len(to_move) == 0 and fi == 0
    if end_of_turn do
        state.turn += 1
    end

    if end_of_turn and rem(state.turn, 4) == 0 do
        state.score += graph.scoreGraph' state.graph
    end

    if len(to_move) == 0 do
        i := 0
        loop i < len(state.graph.nodes) do
            spawn :: fn faction do
                if faction == state.player_turn do
                    guys -> push' guy.new_guy(faction, i)
                end
            end

            case state.graph.nodes[i].tile do
                Base faction do
                    spawn' faction
                end
                Factory faction do
                    spawn' faction
                end
                else do end
            end
            i = i + 1
        end

        state.guys_to_move[fi] = guys -> clone'
        shuffle' state.guys_to_move[fi]
    end
end


makeFullscreen :: fn do
    w := l.WindowFlags {
        fullscreen: true,
        fullscreentype: "desktop",
        vsync: 0,
        msaa: 2,
        resizable: false,
        borderless: false,
        centered: false,
        display: 0,
        minwidth: 100,
        minheight: 100,
    }
    love.window.setMode' 0, 0, w
end

States :: enum
    MainMenu menu.Menu,
    Game GameState,
end

InputState :: enum
    MoveGuy,
    MoveGuyDirection (float, float), // direction xy
end

GameState :: blob {
    graph: graph.Graph,
    guys: {int: [guy.Guy]},
    guys_to_move: {int: [guy.Guy]},
    score: (int, int, int, int),
    turn: int,
    factions: [guy.Faction],
    player_turn: guy.Faction,
    moves_left: int,
    inputState: InputState,
}

send_shader_graph_positions :: fn shader: Shader, points do
    // !!!!!!!! DON'T DO THIS AT HOME !!!!!!!
    do
        v : [int] : [
            unsafe_force(shader),
            unsafe_force("vertex_positions"),
        ]
        points -> for_each' fn x do
            v -> push' unsafe_force' (x[0], x[1])
        end
        varargs(shader.send)(v)
    end

    do
        v : [int] : [
            unsafe_force(shader),
            unsafe_force("vertex_colors"),
        ]
        points -> for_each' fn x do
            v -> push' unsafe_force' x[2]
        end
        varargs(shader.send)(v)
    end

    do
        v :: [
            unsafe_force(shader),
            unsafe_force("num_vertex_positions"),
            len(points),
        ]
        varargs(shader.send)(v)
    end
end

range :: fn max ->
    nums :: []

    i := 0
    loop i < max do
        nums -> push' i
        i += 1
    end

    ret nums
end

new_game :: fn num_players ->
    factions :: []
    range(num_players) -> for_each' fn i do
        factions -> push' guy.int_to_faction' i
    end

    graph :: graph.createGraph()

    guys :: {:}
    guys_to_move :: {:}

    factions -> for_each' fn faction do
        fi :: guy.faction_to_int' faction
        guys[fi] = []

        range(len(graph.nodes)) -> for_each' fn i do
            node :: graph.nodes[i]

            case node.tile do
                Base f do
                    if f == faction do
                        g :: guy.new_guy' faction, i
                        guys[fi] -> push' g
                    end
                end
                else do end
            end
        end

        guys_to_move[fi] = guys[fi] -> clone'
    end

    GameState {
        graph: graph,
        guys: guys,
        guys_to_move: guys_to_move,
        factions: factions,
        player_turn: guy.Faction.Blueberry,
        score: (0, 0, 0, 0),
        turn: 1,
        moves_left: 1,
        inputState: InputState.MoveGuy,
    }
end

playerIndex :: fn state -> do
    fis := state.factions -> map' fn faction -> do
        faction_to_int' faction
    end

    i := 0
    loop i < len(fis) do
        if fis[i] == faction_to_int(state.player_turn) do break end
        i += 1
    end
    ret i
end

init :: fn do
    makeFullscreen'

    ctrl.initControllers' 4

    res.loadResources()

    defaultFont :: gfx.createFont(love.graphics.getFont())
    mainMenuFont :: gfx.createFont(res.font["menu"])

    scene := States.MainMenu menu.createMenu([])
    scene = States.MainMenu menu.createMenu([
        ("2 Players", fn do
            scene = States.Game new_game' 2
        end),
        ("3 Players", fn do
            scene = States.Game new_game' 3
        end),
        ("4 Players", fn do
            scene = States.Game new_game' 4
        end),
        ("Exit", fn do
            love.event.quit' 0
        end)
    ])

    update :: fn delta do
        case scene do
            MainMenu _ do
            end
            Game state do
                graph.updateGraph' state.graph, delta
                graph.scoreGraph' state.graph

                // Update animation timers
                state.factions -> for_each' fn f do
                    guys :: state.guys_to_move[guy.faction_to_int(f)]
                    guys -> for_each' fn g do
                        guy.updateGuy' g, delta
                    end
                end

                deadzone :: 0.5

                // Inputs
                case state.inputState do
                    MoveGuy do
                        may_dir := inputDirection' state
                        case may_dir do
                            Just dir do
                                if magnitude(dir) > deadzone do
                                    state.inputState = InputState.MoveGuyDirection dir
                                end
                            end
                            else do end
                        end

                        pi := playerIndex' state
                        if ctrl.playerControllers[pi].finish do
                            next_turn' state
                        end
                    end

                    MoveGuyDirection old_dir do
                        new_dir := old_dir

                        may_dir := inputDirection' state
                        case may_dir do
                            Just dir do
                                if magnitude(dir) > deadzone do
                                    state.inputState = InputState.MoveGuyDirection dir
                                    new_dir = dir
                                end
                            end
                            else do end
                        end

                        // Move guy
                        pi := playerIndex' state
                        if ctrl.playerControllers[pi].move do
                            move_active_guy' state, new_dir
                        end

                        if ctrl.playerControllers[pi].finish do
                            next_turn' state
                        end
                    end

                    else do end
                end

            end
            else do <!> end
        end

        ctrl.postTick'
    end

    voronoiShader :: love.graphics.newShader("res/voronoi.glsl")

    draw :: fn do
        drawVoronoiBackground :: fn vertices: [(*, *, *)] do
            love.graphics.setShader' voronoiShader
            send_shader_graph_positions' voronoiShader, vertices
            gfx.drawRect' WHITE, (0., 0.), (as_float(love.graphics.getWidth'), as_float(love.graphics.getHeight'))
            gfx.clearShader'
        end

        case scene do
            MainMenu menu do
                gfx.drawRect' BACKGROUND_COLOR, (0., 0.), gfx.screenSizef()

                MENU_SIZE :: 1.
                MIDDLE :: gfx.screenSizef() / (2., 2.)

                // Header
                headerBox :: (600.0, 0.0)
                mainMenuFont.drawTextFT(
                    HEADER_COLOR,
                    "Age of Sylt",
                    MIDDLE  - headerBox / (2., 2.) - (0., 250.),
                    gfx.Alignment.Center,
                    600.0,
                    0.0,
                    MENU_SIZE
                )

                // Choices
                choiceBox :: (300.0, 0.0)
                offset := (0.0, -80.)
                i := 0
                menu.choices -> for_each' fn choice do
                    color := MENU_COLOR
                    if i == menu.position do
                        color = SELECTED_COLOR
                    end

                    mainMenuFont.drawTextFT(
                        color,
                        choice[0],
                        MIDDLE - choiceBox / (2., 2.) + offset,
                        gfx.Alignment.Center,
                        choiceBox[0],
                        0.0,
                        MENU_SIZE
                    )
                    offset += (0.0, 80.0)
                    i += 1
                end

                // Crests
                B_OFFEST :: (300., 0.)
                C_OFFSET :: (-300., 0.)
                G_OFFSET :: (200., 300.)
                S_OFFSET :: (-200., 300.)
                SCALE :: (2.0, 2.0)
                guy.crest_image(guy.Faction.Blueberry).drawCentered' MIDDLE + B_OFFEST, 0.0, SCALE
                guy.crest_image(guy.Faction.Cloudberry).drawCentered' MIDDLE + C_OFFSET, 0.0, SCALE
                guy.crest_image(guy.Faction.Gooseberry).drawCentered' MIDDLE + G_OFFSET, 0.0, SCALE
                guy.crest_image(guy.Faction.Strawberry).drawCentered' MIDDLE + S_OFFSET, 0.0, SCALE
            end

            Game state do
                offset :: graph.graphCenter' state.graph

                state.graph.nodes -> map(fn node ->
                    (
                        node.position[0] + offset[0],
                        node.position[1] + offset[1],
                        graph.shaderColor(node)
                    )
                end) -> drawVoronoiBackground'

                love.graphics.translate' offset[0], offset[1]
                do
                    graph.drawGraph' state.graph

                    state.factions -> for_each' fn f do
                        guys :: state.guys[guy.faction_to_int(f)]
                        guys -> for_each' fn g do
                            guy.drawGuy' g, state.graph
                        end
                    end

                    case state.inputState do
                        MoveGuyDirection dir do
                            g :: state -> guy_to_move'
                            facing :: graph.facingNeighbour' g.node, count_guys_of_faction(state), g.faction, dir, state.graph
                            if facing != -1 do
                                guy.drawGuyDirection' g, facing, state.graph
                            end
                        end
                        else do end
                    end
                end
                love.graphics.origin'

                s :: as_str' state.score
                mainMenuFont.drawText' WHITE, s, (100.0, 50.0)
            end

            else do <!> end
        end
    end

    love.keypressed = fn _key, scancode, repeat do
        key := input.strToKeyDict[_key]
        case key do
            Escape do
                print' "Quitting game..."
                love.event.quit' 0
            end else do end
        end

        case scene do
            MainMenu menu do
                case key do
                    Return do
                        menu.select()
                    end
                    Space do
                        menu.select()
                    end
                    Up do
                        menu.up()
                    end
                    Down do
                        menu.down()
                    end
                    else do end
                end
            end
            Game state do
                case state.inputState do
                    MoveGuyDirection dir do
                        pi := playerIndex' state
                        case key do
                            Space do
                                ctrl.playerControllers -> for_each' fn controller do
                                    controller.move = true
                                end
                                ctrl.playerControllers[pi].move = true
                            end
                            Return do
                                ctrl.playerControllers -> for_each' fn controller do
                                    controller.finish = true
                                end
                            else do end
                        end
                    end else do end
                end
            end else do <!> end
        end
    end

    love.keyreleased = fn key, scancode do
    end

    love.update = update
    love.draw = draw
end

Maybe :: enum
    Just *,
    Nothing,
end

inputDirection :: fn state -> Maybe do // Maybe (float, float)
    res := (0., 0.)
    if input.isKeyDown' input.Key.Up do res += (0., -1.) end
    if input.isKeyDown' input.Key.Down do res += (0., 1.) end
    if input.isKeyDown' input.Key.Left do res += (-1., 0.) end
    if input.isKeyDown' input.Key.Right do res += (1., -0.) end
    if res == (0., 0.,) do
        ret Maybe.Just ctrl.playerControllers[playerIndex(state)].joystick
    end


    ret Maybe.Just (normalize' res)
end
